# CMake configuration file for mold
#
# This file intends to help users to get started with mold easily. Existing
# CMake projects can switch to mold with just a few lines like this:
#
#   find_package(mold CONFIG)
#   if(mold_FOUND)
#     target_use_mold(<target-name>)
#   endif()
#
# That sets mold as the linker for the target specified.
#
# To make sure its relocatability, this configure file is generated by CMake's
# configure_package_config_file command from CMakePackageConfigHelpers module.
# With its help, this config file can function correctly even if the prefix is
# changed during installation (using `cmake --install . --prefix <dir>`),
# because it only sees the prefix (referenced as PACKAGE_PREFIX_DIR) as a path
# relative to its own location.
#
# To avoid relocation issues, all path variables related to installation
# directories in this file should be computed by configure_package_config_file.
# This could be done by adding the variable to PATH_VARS in the
# configure_package_config_file invocation in CMakeLists.txt, and referencing
# them here as PACKAGE_<path-variable>, e.g. PACKAGE_CMAKE_INSTALL_BINDIR.

# This PACKAGE_INIT will be expanded by command configure_package_config_file
# and should be placed before all PACKAGE_* variables.
@PACKAGE_INIT@

# Sets mold as the linker for the target.
function(target_use_mold TARGET)
  if(APPLE)
    target_link_options(${TARGET} PRIVATE
      -fuse-ld=@PACKAGE_CMAKE_INSTALL_BINDIR@/ld64.mold)
  else()
    target_link_options(${TARGET} PRIVATE
      -B@PACKAGE_CMAKE_INSTALL_LIBEXECDIR@/mold)
  endif()
endfunction()
